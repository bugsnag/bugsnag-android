plugins {
    id "com.android.library"
    id "kotlin-android"
}

apply plugin: "io.gitlab.arturbosch.detekt"
apply plugin: "org.jlleitschuh.gradle.ktlint"

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 14
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    kotlinOptions {
        jvmTarget = "1.6"
    }
    lintOptions {
        tasks.lint.enabled = false
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    if (project.hasProperty("TEST_FIXTURE_NDK_VERSION")) {
        def version = project.property("TEST_FIXTURE_NDK_VERSION")
        project.logger.lifecycle("Building module with custom NDK version '$version'")
        ndkVersion = version
    } else {
        ndkVersion = "17.2.4988734"
        project.logger.lifecycle("Building module with default NDK version ($ndkVersion)")
    }
}

dependencies {
    implementation project(":jvm-scenarios")
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

apply from: "../bugsnag-dependency.gradle"
apply from: "../../../../gradle/detekt.gradle"
apply from: "../../../../gradle/checkstyle.gradle"
