apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "io.gitlab.arturbosch.detekt"
apply plugin: "org.jlleitschuh.gradle.ktlint"

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 30
        versionCode 34
        versionName "1.1.14"
        manifestPlaceholders = [
                // omit any of the following placeholders to use the default values
                bugsnagApiKey: "abc12312312312312312312312312312",
                bugsnagAppType: "test",
                bugsnagAppVersion: "7.5.3",
                bugsnagAutoDetectErrors: true,
                bugsnagAutoTrackSessions: false,
                bugsnagBuildUuid: "test-7.5.3",
                bugsnagDiscardClasses: "com.domain.ClassName,com.domain.SecondClassName",
                bugsnagEnabledReleaseStages: "production,development,testing",
                bugsnagEndpoint: "http://bs-local.com:9339/notify",
                bugsnagEndpointSessions: "http://bs-local.com:9339/sessions",
                bugsnagLaunchCrashThresholdMs: 10000,
                bugsnagMaxBreadcrumbs: 1,
                bugsnagPersistUser: false,
                bugsnagProjectPackages: "java.net.UnknownHostException,com.example.Custom",
                bugsnagRedactedKeys: "filter_me",
                bugsnagReleaseStage: "testing",
                bugsnagSendThreads: "always",
                bugsnagVersionCode: 753,
        ]
    }
    signingConfigs {
        release {
            keyAlias "password"
            keyPassword "password"
            storeFile file("../../fakekeys.jks")
            storePassword "password"
        }
    }
    buildTypes {
        release {
            minifyEnabled true // obfuscation disabled to simplify maze
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }

    // adding custom jniLibs results in dupe SO files, ignore build error by
    // picking SO file. see https://developer.android.com/studio/releases/gradle-plugin#cmake-imported-targets
    packagingOptions {
        pickFirst "**/*.so"
    }
    lintOptions {
        tasks.lint.enabled = false
    }

    if (project.hasProperty("TEST_FIXTURE_NDK_VERSION")) {
        def version = project.property("TEST_FIXTURE_NDK_VERSION")
        project.logger.lifecycle("Building test fixture with custom NDK version '$version'")
        ndkVersion = version
    } else {
        project.logger.lifecycle("Building test fixture with default NDK version")
        ndkVersion = "16.1.4479499"
    }

    // conditionally alter the filename depending on the fixture
    applicationVariants.all { variant ->
        variant.outputs.all {
            if (project.hasProperty("TEST_FIXTURE_NAME")) {
                outputFileName = project.property("TEST_FIXTURE_NAME")
            } else {
                outputFileName = "fixture.apk"
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    kotlinOptions {
        jvmTarget = "1.6"
    }
}

dependencies {
    implementation project(":jvm-scenarios")
    implementation project(":cxx-scenarios")

    if (project.hasProperty("MINIMAL_FIXTURE") && "true" == project.property("MINIMAL_FIXTURE")) {
        // don't include NDK scenarios that require linking against bugsnag-plugin-android-ndk
    } else {
        implementation project(":cxx-scenarios-bugsnag")
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.annotation:annotation:1.1.0"
}

apply from: "../bugsnag-dependency.gradle"
apply from: "../../../../gradle/detekt.gradle"
apply from: "../../../../gradle/checkstyle.gradle"
