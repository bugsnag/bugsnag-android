plugins {
    id "com.android.library"
    id "kotlin-android"
}

apply plugin: "io.gitlab.arturbosch.detekt"
apply plugin: "org.jlleitschuh.gradle.ktlint"

android {
    compileSdkVersion 35

    defaultConfig {
        minSdkVersion 17
        manifestPlaceholders = [
                bugsnagPerformanceApiKey: System.getenv("BUGSNAG_PERFORMANCE_API_KEY")
                        ?: "abc12312312312312312312312312312",
        ]
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    lintOptions {
        tasks.lint.enabled = false
    }
    namespace 'com.bugsnag.android.mazerunner.jvmscenarios'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    if (useLegacyOkHttp()) {
        project.logger.lifecycle("Using OkHttp 3 dependency in test fixture")
        implementation "com.squareup.okhttp3:okhttp:3.12.0"
    } else {
        project.logger.lifecycle("Using OkHttp 4 dependency in test fixture")
        implementation "com.squareup.okhttp3:okhttp:4.12.0"
    }
    implementation "com.bugsnag:bugsnag-android-performance:1.2.2"
}

private boolean useLegacyOkHttp() {
    project.hasProperty("USE_LEGACY_OKHTTP") && "true" == project.property("USE_LEGACY_OKHTTP")
}

apply from: "../bugsnag-dependency.gradle"
apply from: "../../../../gradle/detekt.gradle"
apply from: "../gradle/checkstyle.gradle"
