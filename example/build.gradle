buildscript {
    repositories {
        google()
        jcenter()
        mavenLocal()
    }
    dependencies {
        classpath 'com.bugsnag:bugsnag-android-gradle-plugin:3.1.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

repositories {
    google()
    jcenter()
}

android {

    compileSdkVersion Integer.parseInt(project.ANDROID_COMPILE_SDK_VERSION)

    defaultConfig {
        minSdkVersion Integer.parseInt(project.ANDROID_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_TARGET_SDK_VERSION)

        manifestPlaceholders = [
            bugsnagApiKey                    : "your-api-key",
            bugsnagBuildUUID                 : "abc123",
            bugsnagAppVersion                : "1.0.0",
            bugsnagEndpoint                  : "https://notify.bugsnag.com",
            bugsnagSessionsEndpoint          : "https://sessions.bugsnag.com",
            bugsnagReleaseStage              : "debug",
            bugsnagSendThreads               : true,
            bugsnagEnableExceptionHandler    : true,
            bugsnagPersistUserBetweenSessions: false
        ]
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.pro'
        }
    }
    splits {
        density {
            enable true
        }
    }

    flavorDimensions "regular"

    splits {
        density {
          enable true
          reset()
          include "mdpi", "hdpi"//, "xhdpi", "xxhdpi", "xxxhdpi"
        }
        abi {
          enable true
          reset()
          include 'armeabi-v7a', 'x86'
          universalApk true
        }
    }

    productFlavors {
        javaExample {
            applicationIdSuffix ".java"
        }
        kotlinExample {
            applicationIdSuffix ".kotlin"
        }
    }
    lintOptions {
        disable 'OnClick'
        abortOnError true
    }
}

dependencies {
    implementation project(":sdk")
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:support-v4:$supportLibVersion"
    kotlinExampleImplementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    androidTestImplementation "com.android.support.test:runner:$supportTestVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation "com.android.support.test:rules:$supportTestVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}

apply plugin: 'com.bugsnag.android.gradle'

apply plugin: 'checkstyle'

checkstyle {
    toolVersion = "6.16"
}
task("checkstyle", type: Checkstyle) {
    configFile rootProject.file("config/checkstyle/checkstyle.xml")
    source "src/javaExample/java"
    include "**/*.java"
    classpath = files()
}
